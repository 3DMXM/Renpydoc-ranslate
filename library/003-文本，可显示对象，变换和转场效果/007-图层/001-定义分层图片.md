# 图层
当一个人物的立绘组合到达一定的复杂程度以后，定义所有的可能性就显得有点傻气。比如一个立绘有4种穿着，4种发型，6种表情，这就已经组合出96种可能性了。为每一种可能性改变一张图片文件会大量占用硬盘空间以及时间。    

为了提高效率，Ren'py有一种可以定义多图层图片的方法。（可以想象一下这里的层就是像图像处理软件 Photoshop或是GIMP的图层一样，而不是在Ren'py其他地方里面用到的层（layers）。）层可以不加条件地显示，或可以被一些提供给图片的特性或是运行时刻的评估条件所选择。     

为了更容易地定义分层图片， Ren'py提供一种 layeredimage 语句， 它引入了一种领域专用语言（DSL），可以使你定义分层图片。 这里也有 LayeredImage() 对象， 它不是个图片但可以分配给 image 语句并且像图片一样使用。    

## 定义分层图片
分层图片领域专用语言（DSL）包含很少的语句，其中一个同时是一种脚本语言语句，它可以让你引入图片，后面跟着引入图层和图层组的语句。    

为了介绍这种语言，这里有一组分层图片使用了这些特性。    
```python
layeredimage augustina:

    always:
        "augustina_base"

    group outfit:

        attribute dress:
            "augustina_outfit_dress"

        attribute jeans:
            "augustina_outfit_jeans"

    group eyes:

        attribute open default:
            "augustina_eyes_open"
            default True

        attribute wink:
            "augustina_eyes_wink"

    group eyebrows:

        attribute normal default:
            "augustina_eyebrows_normal"

        attribute oneup:
            "augustina_eyebrows_oneup"

    group mouth:

        pos (100, 100)

        attribute smile default:
            "augustina_mouth_smile"

        attribute happy:
            "augustina_mouth_happy"

    if evil:
        "augustina_glasses_evil"
    else:
        "augustina_glasses"

```
这是一段很长的脚本，但是它很有规律，并且后面我们将展示如何简化它。    

首先，layeredimage语句引入一个分层图片，名字为角色名。这个语句是Ren'py脚本语句的一部分，在初始化时就运行。   

分层图片的代码块可以包含 always，group 和 if 语句。 group语句可以带特性（attribute）。always和if语句后面必须为可显示对象，特性（attribute）是可选内容。所有的语句可以带属性（properties）。    

always 语句申明了总是会显示的图层，就像一个立绘的“素体”。    

group 语句引入了一组特性（attributes），只有一种特性可以在用同一时间显示出来。所以这组图片只能显示一套穿着，一组眼睛，一组眉毛和一张嘴。这个组的属性（properties）传递给特性（attributes），组可以自动声明特性。    

attribute 语句引入了一个层，如果图片带着这个特性（attribute）后就会显示出来。比如“augustina_outfit_dress”只有在“dress”特性出现时才会显示。如果加上了default关键字，当没有其他特性冲突的时候，这个特性就会默认显示；在这个例子中，“augustina_eyes_open”会默认显示，除非申明了“wink”特性。    

最后，if语句添加了一层，利用python语句在可显示对象间选择。它将被持续判断，当第一个条件判断为真时会进行显示。    

属性包含了属性名和一个简单表达，可以跟在各层后面。 一些属性改变了语句的作用。如果出现一个或多个 transform属性，就会生成一个==Transform()==来包括整个立绘。at属性可以跟在transform或一组transform之后，也可以包括整个可显示对象。比如这里pos属性生成了一个transform，移动所有嘴图片的左上角去指定位置。   

最后总图片的大小是各层图片大小之和，所以也许可以放一层最大的图片在下面，其他层都不会跑到这层外面。第一层是底层，最后一层是顶层——在本例中，眼镜将会在所有图层的上面。    

组（group） 和 特性（attribute） 在分层图片中会出现多次，每一层都带有特性以用来显示。    
