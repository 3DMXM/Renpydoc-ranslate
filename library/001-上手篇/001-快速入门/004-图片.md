一个ADV游戏（or Visual Novel）如果没有了图片那也只能当电子小说看。这里有“The Question”里的一段片段，它包含了如何向用户展示图片的语句。如果你想尝试一下，我们可以用它将原先的脚本完全替换。    
```Python
define s = Character('Sylvie', color="#c8ffc8")
define m = Character('Me', color="#c8c8ff")

label start:

    scene bg meadow

    "After a short while, we reach the meadows just outside the neighborhood where we both live."

    "It's a scenic view I've grown used to. Autumn is especially beautiful here."

    "When we were children, we played in these meadows a lot, so they're full of memories."

    m "Hey... Umm..."

    show sylvie green smile

    "She turns to me and smiles. She looks so welcoming that I feel my nervousness melt away."

    "I'll ask her...!"

    m "Ummm... Will you..."

    m "Will you be my artist for a visual novel?"

    show sylvie green surprised

    "Silence."

```
这部分代码有两个新的语句。第六行的scene语句将清除所有的图片并显示背景图片。16行和26行分别表示在背景上显示立绘，并且变更立绘。    
在Ren'py里每张图片都有名字。名字包含了tag和一个或多个属性。所有tag和属性必须由字母开始，并包含字母，数字，下划线。例如：    
  * 第6行中，tag为“bg”，属性为“meadow”。为了方便起见，通常背景图片的命名包含bg tag。
  * 第16行的第一个show语句中，tag为“sylvie”，属性为“green”和“smile”。
  * 第26行的第二个show语句中，tag为“sylvie”，属性为“green”和“surprised”。

所有tag相同的图片只能同时在画面上显示一张。当第二张带有相同tag的图片要显示时，它将会替换第一张图片，就像第26行那样。    

Ren'py会搜索在image目录下的所有图片，你可以通过在“打开目录”列表中点击“image”来浏览该文件夹。Ren'py的立绘格式要求为PNG或WEBP文件，背景图片为JPG,JPEG,PNG或WEBP文件。文件的取名相当重要，Ren'py将使用除去扩展名后的强制字母变为小写文件名来作为图片名。     

举个例子，以下放在image文件夹下的文件，定义了这些图片名：
  * "bg meadow.jpg" -> `bg meadow`
  * "sylvie green smile.png" -> `sylvie green smile`
  * "sylvie green surprised.png" -> `sylvie green surprised`

以上文件名里字母都为小写，我们来看下面的例子：
  * "Sylvie Green Surprised.png" -> `sylvie gree surprised`

图片可以放在image文件夹的子文件夹中。子文件夹名将会被忽略，只有文件名会用来定义图片名。    

__hide 语句。__ Ren'py提供hide语句来隐藏图片。   
```Python
label leaving:

    s "I'll get right on it!"

    hide sylvie

    "..."

    m "That wasn't what I meant!"
```
要用到hide的情况比较少见。show可以用来进行立绘表情转换，scene可以在所有人离场的时候使用。当只有角色离开，而背景不变的时候我们可以使用。    

__image 语句。__  有时候，制作者不希望Ren'py自动定义图片，这时候就要用到image语句。它应该在脚本的最上方（不缩进，且在start label 之前）就行申明。它可以将图片名和图片文件进行关联。    
举个例子：    
```python
image logo = "renpy logo.png"
image eileen happy = "eileen_happy_blue_dress.png"
```
image语句在初始化时就运行，比start label和其他交互游戏脚本还要之前。      
image语句可以用来做其他复杂的操作，在[其他地方](?file=001-上手篇/001-快速入门/004-图片 "图片")进行讨论。    
