## 过渡
在上面的代码中，图片显示和消失是即时的。对我们来说切换地点或角色的进入离开都是很重要的要素，Ren'py支持过渡功能，它能使图片展示加上更多的特效。    

过渡从前一个交互（台词，菜单，过渡）结束之时开始显示，在执行scene，show，hide语句后显示画面的改变。
```python
label start:

    scene bg meadow
    with fade

    "After a short while, we reach the meadows just outside the neighborhood where we both live."

    "It's a scenic view I've grown used to. Autumn is especially beautiful here."

    "When we were children, we played in these meadows a lot, so they're full of memories."

    m "Hey... Umm..."

    show sylvie green smile
    with dissolve

    "She turns to me and smiles. She looks so welcoming that I feel my nervousness melt away."

    "I'll ask her...!"

    m "Ummm... Will you..."

    m "Will you be my artist for a visual novel?"
```
with语句后面跟着需要使用的过渡名。最常用的是`dissolve`，它能使画面慢慢地进行切换。另一个有用的过渡叫`fade`，可以使画面渐渐变黑然后淡入新的画面。    

当一个过渡在多个图片（scene，show，hide语句）后出现时，它将对所有图片一起应用。如果你写成这样：    
```python
scene bg meadow
show sylvie green smile
with dissolve
```
bg meadows和 sylvie都会被渐入。如果想分别作用于两者，你可以写成这样：    
```python
scene bg meadow
with dissolve
show sylvie green smile
with dissolve
```
这里首先将渐入bg meadow，然后渐入sylvie。如果你想迅速显示bg meadow然后显示sylvie，你可以这样写：    
```python
scene bg meadow
with None
show sylvie smile
with dissolve
```
None被用来指明一种特殊的过渡——没有过渡。这其实对用户来说什么过渡也没有，只是更新了Ren'py的内部状态。
